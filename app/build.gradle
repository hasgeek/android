buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.3'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'realm-android'
apply plugin: 'com.github.triplet.play'
apply plugin: 'org.moallemi.advanced-build-version'


advancedVersioning {
    nameOptions {
        versionMajor 1
        versionMinor 1
    }

    codeOptions {
        versionCodeType org.moallemi.gradle.internal.VersionCodeType.AUTO_INCREMENT_DATE
        dependsOnTasks 'release' // defaultValue
    }
    outputOptions {
        renameOutput true
        nameFormat '${appName}-${buildType}-${versionName}'
    }
}

android {
    signingConfigs {
        release {
        }
    }
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "com.hasgeek"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode advancedVersioning.versionCode
        versionName advancedVersioning.versionName
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

ext.supportLibraryVersion = '27.1.1'
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    // Support libraries
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    // Squarify
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    // Reactive Extensions for Android
    implementation 'io.reactivex:rxandroid:1.2.1'
    // RxJava
    implementation 'io.reactivex:rxjava:1.1.7'
    // Realm helpers
    implementation 'io.realm:android-adapters:1.3.0'
    // Logger
    implementation 'com.jakewharton.timber:timber:4.6.0'
    // Gson
    implementation 'com.google.code.gson:gson:2.8.2'
    // QR Code library
    implementation 'me.dm7.barcodescanner:zbar:1.9'
    // Dexter
    implementation 'com.karumi:dexter:2.3.1'
    // Permissions
    implementation 'com.karumi:dexter:2.3.1'
    // Bottom Tab Bar
    implementation 'com.aurelhubert:ahbottomnavigation:1.5.1'
    // Sectioned RecyclerView
    implementation 'com.truizlop.sectionedrecyclerview:library:1.2.0'

    implementation('com.crashlytics.sdk.android:answers:1.3.10@aar') {
        transitive = true;
    }

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
}

play {
    jsonFile = file('../keys/deploycreds.json')
    track = 'production'
}


def props = new Properties()

props.load(new FileInputStream(rootProject.file("keys/release.properties")))

android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword